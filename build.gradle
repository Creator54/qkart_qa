plugins {
    id 'java'
}

group = 'dev.creator54'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // This dependency is used by the application.
    implementation 'com.google.guava:guava:31.1-jre'

    // https://mvnrepository.com/artifact/org.seleniumhq.selenium/selenium-java
    implementation 'org.seleniumhq.selenium:selenium-java:4.9.0'

    // https://mvnrepository.com/artifact/io.github.bonigarcia/webdrivermanager
    implementation 'io.github.bonigarcia:webdrivermanager:5.3.2'

    // https://mvnrepository.com/artifact/com.relevantcodes/extentreports
    implementation 'com.relevantcodes:extentreports:2.41.2'

    // https://mvnrepository.com/artifact/org.testng/testng/7.5
    testImplementation 'org.testng:testng:7.5'
}

task startScript(type:Exec) {
    commandLine './selenium-run.sh'
}

task run(type: JavaExec) {
    main = 'dev.creator54.Main'
    classpath = sourceSets.main.runtimeClasspath
}

test {
    useTestNG() {
        useDefaultListeners = true
        suites "src/test/java/dev/creator54/QKART_TESTNG/testng.xml"
    }

    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTEST RESULT: ${result.resultType}"
            println "TEST SUMMARY: RAN ${result.testCount} TESTS, " +
                    "${result.successfulTestCount} SUCCEEDED, " +
                    "${result.failedTestCount} FAILED, " +
                    "${result.skippedTestCount} SKIPPED"
        }
    }

    testLogging {
        events "PASSED", "SKIPPED", "FAILED", "STANDARD_OUT", "STANDARD_ERROR"
        exceptionFormat = 'full'

        // Optionally do:
        showStackTraces = true
        showStandardStreams = true
    }

    outputs.upToDateWhen {false}
}

tasks.run.dependsOn startScript
test.dependsOn startScript